AC_INIT([pgactive], [pgactive-next], [https://github.com/aws/pgactive/issues/new/choose], [pgactive], [https://github.com/aws/pgactive])
AC_COPYRIGHT([Copyright (c) 1996-2015, PostgreSQL Global Development Group])

AC_SUBST(configure_args, [$ac_configure_args])

AC_ARG_ENABLE([pgactive],
  [AS_HELP_STRING([--enable-pgactive],
    [enable active-active replication [yes/no/auto] @<:@default=auto@:>@])],
  [],
  [enable_pgactive=auto])

AC_PROG_SED

AC_ARG_VAR([PG_CONFIG], [Location to find pg_config for target PostgresQL (default PATH)])

if test -z "$PG_CONFIG"; then
  AC_PATH_PROG(PG_CONFIG, pg_config)
fi

if test -z "$PG_CONFIG"; then
   AC_MSG_ERROR([could not find pg_config, set PG_CONFIG or PATH])
fi

pgac_pg_config_version=$($PG_CONFIG --version 2>/dev/null)
version_num=$(echo "$pgac_pg_config_version"|
              $SED 's/[[^0-9]]//g' | cut -c 1-2)

if test -z "$version_num"; then
  AC_MSG_ERROR([could not detect the PostgreSQL version, wrong or broken pg_config?])
fi

AC_MSG_NOTICE([building against PostgreSQL $version_num])
# Even though pgactive defines MAJORVERSION for this, we need access to it
# early in Makefile, so set it directly.
pgactive_PG_MAJORVERSION="$version_num"
AC_SUBST(pgactive_PG_MAJORVERSION)

# add includedir to prerequisites, so tests for headers can succeed
CPPFLAGS="-I$($PG_CONFIG --includedir-server) $CFLAGS"

# now that we found pg_config, check whether we're building against
# a stock postgres or not.
AC_CHECK_HEADERS(
    [replication/replication_identifier.h replication/origin.h],
    [],
    [],
[#include "postgres.h"
])

AC_CHECK_HEADERS(
    [access/committs.h access/commit_ts.h],
    [],
    [],
[#include "postgres.h"
])

AC_ARG_VAR([PATH], [PATH for target PostgreSQL install pg_config])

AC_MSG_NOTICE([building with PostgreSQL $version_num])
pgactive_PGVERCOMPAT_INCDIR="compat/$version_num"

AC_SUBST(pgactive_PGVERCOMPAT_INCDIR)


# check whether we're building inside the source tree.
if test "$srcdir" -ef '.' ; then
  vpath_build=no
else
  vpath_build=yes
fi
AC_SUBST(vpath_build)

AC_CONFIG_FILES([Makefile])
AC_CONFIG_FILES([test/run_tests], [chmod +x test/run_tests])
AC_CONFIG_HEADERS([include/pgactive_config_generated.h])
AH_TOP([/* Do not include directly, use pgactive_config.h instead. */])
AC_OUTPUT
